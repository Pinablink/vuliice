;----------------------------------------------------------------------------------------------
#include "msp430g2553.h"  
#include "C:/libMSP430/VulIice/header/VulIice_Oper.h"
;----------------------------------------------------------------------------------------------
;----------------------------------------------------------------------------------------------
        NAME    MAIN   
        PUBLIC  MAIN
        EXTERN VULIICE_EXEC_OPER
;----------------------------------------------------------------------------------------------
        RSEG    CSTACK                  
        RSEG    CODE                    
;----------------------------------------------------------------------------------------------
;----------------------------------------------------------------------------------------------
RESET:                  MOV      #SFE(CSTACK), SP
MAIN:                   MOV.W    #WDTPW+WDTHOLD,&WDTCTL
                        ; BLOCO REFERENTE A IMPLEMENTACAO DA LIB. VERIFICA O STATUS RETORNADO PARA TOMADA DE DECISAO
                        CMP.B    #IBYTE_DADOS_RECEBIDOS,&CONF_STATUS_PROCESS
                        JEQ      VERIFICAR_CONTEUDO
                        ;
GO_RUN:                 CALL     #LIMPAR_REGISTRADORES
                        CALL     #LIMPAR_RAM_500BYTES
                        CALL     #CONFIG_CLOCK_16MHZ
                        CALL     #CONFIGURAR_UART_16MHZ
                        CALL     #CONFIGURAR_PORTAS 
                        ; BLOCO REFERENTE A IMPLEMENTA플O DA LIB
                        MOV.B    #OPER_LIMPEZA_CONTADORES_SERIAL,VULIICE_OPER
                        CALL     #VULIICE_EXEC_OPER
                        MOV.W    #IIBYTE_TAMANHO_DADOS,&CONF_LEN_DADOS ;INFORMA AQUI O TAMANHO DOS DADOS QUE SER PROCESSADO NA PORTA SERIAL
                        MOV.B    #OPER_RECEBER_DADOS_SERIAL,VULIICE_OPER
                        ; FIM DO BLOCO REFERENTE A IMPLEMENTA플O DA LIB
                 
CONT_PROCESS:           CALL     #HABILITAR_COMUNICACAO_SERIAL
                        BIS.B    #CPUOFF+GIE,SR
                        NOP
VERIFICAR_CONTEUDO:     MOV.B    #OPER_LIMPEZA_CONTADORES_SERIAL,VULIICE_OPER
                        CALL     #VULIICE_EXEC_OPER
                        MOV.B    #OPER_VALIDAR_CONTEUDO,VULIICE_OPER
                        CALL     #VULIICE_EXEC_OPER
                        CMP.B    #IBYTE_ERRO_FORMATO_DADO_SERIAL,&CONF_STATUS_PROCESS
                        JEQ      D_ERRO_ESTRUTURAL
                        CMP.B    #IBYTE_ERRO_FORMATO_SERIAL_QT_MARK,&CONF_STATUS_PROCESS
                        JEQ      D_ERRO_ESTRUTURAL_QTMK
                        JMP      GOGO
D_ERRO_ESTRUTURAL:      CALL     #LIMPEZA_RECURSOS
                        MOV.B    #OPER_CRIAR_ERRO_ESTRUTURAL_MENSAGEM,VULIICE_OPER
                        CALL     #VULIICE_EXEC_OPER
                        CALL     #AJUSTE_RECURSOS
                        JMP      GOGO
D_ERRO_ESTRUTURAL_QTMK: CALL     #LIMPEZA_RECURSOS
                        MOV.B    #OPER_CRIAR_ERRO_ESTRUTURAL_QTMK_MENSAGEM,VULIICE_OPER
                        CALL     #VULIICE_EXEC_OPER
                        CALL     #AJUSTE_RECURSOS
GOGO:                   NOP
                        MOV.B    #OPER_RECEBER_DADOS_SERIAL,VULIICE_OPER
                        MOV.B    #IBYTE_PROCESSO_AGUARDANDO,&CONF_STATUS_PROCESS
                        JMP      CONT_PROCESS
;----------------------------------------------------------------------------------------------
;----------------------------------------------------------------------------------------------
LIMPEZA_RECURSOS
;----------------------------------------------------------------------------------------------
                        MOV.B    #OPER_LIMPEZA_CONTADORES_SERIAL,VULIICE_OPER
                        CALL     #VULIICE_EXEC_OPER
                        MOV.B    #OPER_EXCLUIR_MENSAGEM_SERIAL_MEM_RAM,VULIICE_OPER
                        CALL     #VULIICE_EXEC_OPER     
                        MOV.B    #OPER_LIMPEZA_CONTADORES_SERIAL,VULIICE_OPER
                        CALL     #VULIICE_EXEC_OPER
                        RET
;----------------------------------------------------------------------------------------------
;----------------------------------------------------------------------------------------------
AJUSTE_RECURSOS
;----------------------------------------------------------------------------------------------
                        MOV.B    #OPER_LIMPEZA_CONTADORES_SERIAL,VULIICE_OPER
                        CALL     #VULIICE_EXEC_OPER
                        MOV.B    #OPER_RETORNAR_ERRO_ESTRUTURA_SERIAL,VULIICE_OPER
                        CALL     #VULIICE_EXEC_OPER
                        MOV.B    #OPER_LIMPEZA_CONTADORES_SERIAL,VULIICE_OPER
                        CALL     #VULIICE_EXEC_OPER
                        MOV.B    #OPER_EXCLUIR_MENSAGEM_MEM_RAM,VULIICE_OPER
                        CALL     #VULIICE_EXEC_OPER
                        MOV.B    #OPER_LIMPEZA_CONTADORES_SERIAL,VULIICE_OPER
                        CALL     #VULIICE_EXEC_OPER
                        MOV.B    #000H,&LEN_SERIAL_MESSAGE
                        MOV.B    #000H,&LEN_I_MESSAGE
                        RET
;----------------------------------------------------------------------------------------------
;----------------------------------------------------------------------------------------------
LIMPAR_RAM_500BYTES; 
;----------------------------------------------------------------------------------------------
                MOV.W    #01F4H,R4
CONTINUE_1:     TST      R4
                JZ       END_1
                MOV.B    #0B0H,0200H(R5)
                DEC.W    R4
                INC.W    R5
                JMP      CONTINUE_1
END_1:          NOP                
                RET 
;----------------------------------------------------------------------------------------------
;----------------------------------------------------------------------------------------------
CONFIGURAR_PORTAS ;
;----------------------------------------------------------------------------------------------
                MOV.B    #000H,&P1OUT
                BIS.B    #BIT1+BIT2,&P1SEL
                BIS.B    #BIT1+BIT2,&P1SEL2
                BIS.B    #BIT0+BIT6,&P1DIR
                BIC.B    #BIT0+BIT6,&P1OUT
                RET 
;----------------------------------------------------------------------------------------------
;----------------------------------------------------------------------------------------------
CONFIG_CLOCK_16MHZ
;----------------------------------------------------------------------------------------------
                CLR.B    &DCOCTL
                MOV.B    &CALBC1_16MHZ,&BCSCTL1
                MOV.B    &CALDCO_16MHZ,&DCOCTL
                RET
;---------------------------------------------------------------------------------------------- 
;----------------------------------------------------------------------------------------------
CONFIGURAR_UART_16MHZ ;
;----------------------------------------------------------------------------------------------
                MOV.B    #001H, UCA0CTL1
                MOV.B    #000H, UCA0CTL0
                MOV.B    #081H, UCA0CTL1
                MOV.B    #082H, UCA0BR0
                MOV.B    #006H, UCA0BR1
                MOV.B    #00CH, UCA0MCTL
                MOV.B    #080H, UCA0CTL1
                RET 
;----------------------------------------------------------------------------------------------
;----------------------------------------------------------------------------------------------
LIMPAR_REGISTRADORES ;LIMPA OS REGISTRADORES DE USO GERAL
;----------------------------------------------------------------------------------------------
                MOV.W     #00000H,R4
                MOV.W     #00000H,R5
                MOV.W     #00000H,R6
                MOV.W     #00000H,R7
                MOV.W     #00000H,R11
                MOV.W     #00000H,R12
                MOV.W     #00000H,R13
                MOV.W     #00000H,R14
                MOV.W     #00000H,R15
                RET 
;----------------------------------------------------------------------------------------------
;----------------------------------------------------------------------------------------------
TRANSMISSAO
;----------------------------------------------------------------------------------------------
                BIC.B       #UCA0TXIFG,&IFG2 ; BAIXA O FLAG DE COMUNICA플O
                RETI
;----------------------------------------------------------------------------------------------
;----------------------------------------------------------------------------------------------
HABILITAR_COMUNICACAO_SERIAL
;----------------------------------------------------------------------------------------------
                BIC.B    #UCSWRST,&UCA0CTL1
                BIS.B    #UCA0RXIE,&IE2
                BIS.B    #UCA0TXIE,&IE2
                RET
;----------------------------------------------------------------------------------------------
;----------------------------------------------------------------------------------------------
RECEPCAO
;----------------------------------------------------------------------------------------------           
HOLD_ON:            BIT.B    #UCA0RXIFG,&IFG2    ;  RX Buffer ready
                    JZ       HOLD_ON 
                    CALL     #VULIICE_EXEC_OPER
                    CMP.B    #IBYTE_DADOS_RECEBIDOS,&CONF_STATUS_PROCESS
                    JEQ      RECEP_FINALIZADO
                    JMP      CONTINUE_P
RECEP_FINALIZADO:   MOV.W    #000H,VULIICE_OPER
                    BIC.B    #UCA0RXIE,&IE2
                    BIC      #CPUOFF+OSCOFF+SCG0+SCG1,0(SP)               
CONTINUE_P:         RETI
;----------------------------------------------------------------------------------------------
;----------------------------------------------------------------------------------------------
        COMMON      INTVEC
;----------------------------------------------------------------------------------------------
                ORG         RESET_VECTOR
                DW          RESET
                ORG         USCIAB0TX_VECTOR
                DW          TRANSMISSAO
                ORG         USCIAB0RX_VECTOR 
                DW          RECEPCAO
        END
;----------------------------------------------------------------------------------------------
