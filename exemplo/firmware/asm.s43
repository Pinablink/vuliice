;----------------------------------------------------------------------------------------------
#include "msp430g2553.h"  
#include "C:/libMSP430/VulIice/header/VulIice_Oper.h"
;----------------------------------------------------------------------------------------------
;----------------------------------------------------------------------------------------------
        NAME    MAIN   
        PUBLIC  MAIN
        EXTERN PROCESSAMENTO_ENTRADA, VALIDAR_CONTEUDO, EXCLUIR_MENSAGEM, CRIAR_ERROR_ESTRUTURAL_MSG, CRIAR_ERROR_ESTRUTURAL_QT_MARKS, LIMPA_SERIAL_CONTADORES, EXCLUIR_MENSAGEM_ENVIADA, RETORNO_MENSAGEM, CRIAR_MENSAGEM_JSON_INEXISTENTE, CRIAR_MENSAGEM_VIRGULA_FINALIZACAO_ERRO, CRIAR_MENSAGEM_OBJETO_INTERNO_ERRO, CRIAR_MENSAGEM_RETORNO_SUCESSO, CRIAR_MENSAGEM_LINHA_NO_CHAVE, CRIAR_MENSAGEM_CARACTER_NAO_COMPATIVEL
;----------------------------------------------------------------------------------------------
        RSEG    CSTACK                  
        RSEG    CODE                    
;----------------------------------------------------------------------------------------------
;----------------------------------------------------------------------------------------------
RESET:                  MOV      #SFE(CSTACK), SP
MAIN:                   MOV.W    #WDTPW+WDTHOLD,&WDTCTL
                        CMP.B    #IBYTE_ERRO_FORMATO_DADO_SERIAL,&CONF_STATUS_PROCESS
                        JEQ      RUN_
                        CMP.B    #IBYTE_ERRO_FORMATO_SERIAL_QT_MARK,&CONF_STATUS_PROCESS
                        JEQ      RUN_
                        CMP.B    #IBYTE_DADOS_RECEBIDOS,&CONF_STATUS_PROCESS
                        JEQ      RUN_
                        CALL     #LIMPAR_REGISTRADORES
                        CALL     #LIMPAR_RAM_500BYTES
                        CALL     #CONFIGURAR_PORTAS 
RUN_:                   CALL     #CONFIG_CLOCK_16MHZ
                        CALL     #CONFIGURAR_UART_16MHZ
                        ; BLOCO REFERENTE A IMPLEMENTA플O DA LIB
                        MOV.W    #IIBYTE_TAMANHO_DADOS,&CONF_LEN_DADOS ;INFORMA AQUI O TAMANHO DOS DADOS QUE SER PROCESSADO NA PORTA SERIAL
                        MOV.B    #IBYTE_PROCESSO_AGUARDANDO,&CONF_STATUS_PROCESS
                        MOV.B    #000H,&0208H
                        MOV.B    #000H,&0209H
                        CALL     #LIMPA_SERIAL_CONTADORES
                        ; FIM DO BLOCO REFERENTE A IMPLEMENTA플O DA LIB
OK:                     CALL     #HABILITAR_COMUNICACAO_SERIAL
                        BIS.B    #CPUOFF+GIE,SR
                        NOP
                        JMP      RESET
                        NOP
;----------------------------------------------------------------------------------------------
;----------------------------------------------------------------------------------------------
LIMPAR_RAM_500BYTES; 
;----------------------------------------------------------------------------------------------
                MOV.W    #01F4H,R4
CONTINUE_1:     TST      R4
                JZ       END_1
                MOV.B    #0B0H,0200H(R5)
                DEC.W    R4
                INC.W    R5
                JMP      CONTINUE_1
END_1:          NOP                
                RET 
;----------------------------------------------------------------------------------------------
;----------------------------------------------------------------------------------------------
CONFIGURAR_PORTAS ;
;----------------------------------------------------------------------------------------------
                MOV.B    #000H,&P1OUT
                BIS.B    #BIT1+BIT2,&P1SEL
                BIS.B    #BIT1+BIT2,&P1SEL2
                BIS.B    #BIT0+BIT6,&P1DIR
                BIC.B    #BIT0+BIT6,&P1OUT
                RET 
;----------------------------------------------------------------------------------------------
;----------------------------------------------------------------------------------------------
CONFIG_CLOCK_16MHZ
;----------------------------------------------------------------------------------------------
                CLR.B    &DCOCTL
                MOV.B    &CALBC1_16MHZ,&BCSCTL1
                MOV.B    &CALDCO_16MHZ,&DCOCTL
                RET
;---------------------------------------------------------------------------------------------- 
;----------------------------------------------------------------------------------------------
CONFIGURAR_UART_16MHZ ;
;----------------------------------------------------------------------------------------------
                MOV.B    #001H, UCA0CTL1
                MOV.B    #000H, UCA0CTL0
                MOV.B    #081H, UCA0CTL1
                MOV.B    #082H, UCA0BR0
                MOV.B    #006H, UCA0BR1
                MOV.B    #00CH, UCA0MCTL
                MOV.B    #080H, UCA0CTL1
                RET 
;----------------------------------------------------------------------------------------------
;----------------------------------------------------------------------------------------------
LIMPAR_REGISTRADORES ;LIMPA OS REGISTRADORES DE USO GERAL
;----------------------------------------------------------------------------------------------
                MOV.W     #00000H,R4
                MOV.W     #00000H,R5
                MOV.W     #00000H,R6
                MOV.W     #00000H,R7
                MOV.W     #00000H,R12
                MOV.W     #00000H,R13
                MOV.W     #00000H,R14
                MOV.W     #00000H,R15
                RET 
;----------------------------------------------------------------------------------------------
;----------------------------------------------------------------------------------------------
TRANSMISSAO
;----------------------------------------------------------------------------------------------
                BIC.B       #UCA0TXIFG,&IFG2 ; BAIXA O FLAG DE COMUNICA플O
                RETI
;----------------------------------------------------------------------------------------------
;----------------------------------------------------------------------------------------------
HABILITAR_COMUNICACAO_SERIAL
;----------------------------------------------------------------------------------------------
                BIC.B    #UCSWRST,&UCA0CTL1
                BIS.B    #UCA0RXIE,&IE2
                BIS.B    #UCA0TXIE,&IE2
                RET
;----------------------------------------------------------------------------------------------
;----------------------------------------------------------------------------------------------
RECEPCAO
;----------------------------------------------------------------------------------------------           
HOLD_ON:            BIT.B    #UCA0RXIFG,&IFG2    ;  RX Buffer ready
                    JZ       HOLD_ON 
                    CALL     #PROCESSAMENTO_ENTRADA
                    CMP.B    #IBYTE_DADOS_RECEBIDOS,&CONF_STATUS_PROCESS
                    JEQ      RECEP_FINALIZADO
                    JMP      CONTINUE_P
RECEP_FINALIZADO:   BIC.B    #UCA0RXIE,&IE2
                    CALL     #VALIDAR_RETORNO
                    BIC      #CPUOFF+OSCOFF+SCG0+SCG1,0(SP)               
CONTINUE_P:         RETI
;----------------------------------------------------------------------------------------------
;----------------------------------------------------------------------------------------------
VALIDAR_RETORNO
;----------------------------------------------------------------------------------------------
VERIFICAR_CONTEUDO:              CALL     #VALIDAR_CONTEUDO
                                 CMP.B    #IBYTE_ERRO_FORMATO_DADO_SERIAL,&CONF_STATUS_PROCESS
                                 JEQ      D_ERRO_ESTRUTURAL
                                 CMP.B    #IBYTE_ERRO_FORMATO_SERIAL_QT_MARK,&CONF_STATUS_PROCESS
                                 JEQ      D_ERRO_ESTRUTURAL_QTMK
                                 CMP.B    #IBYTE_ERRO_FORMATO_UNKNOW_JSON,&CONF_STATUS_PROCESS
                                 JEQ      D_ERRO_ESTRUTURAL_UNK_JSON
                                 CMP.B    #IBYTE_ERRO_COMMA_CLOSING_NOT_ALLOWED,&CONF_STATUS_PROCESS
                                 JEQ      D_ERRO_ESTRUTURAL_COMMA_CLOSING
                                 CMP.B    #IBYTE_ERRO_IN_OBJ_DUP_CUP,&CONF_STATUS_PROCESS  
                                 JEQ      D_ERRO_ESTRUTURAL_OB_INC_DUP
                                 CMP.B    #IBYTE_ERRO_QUOT_MARKS_UNK_KEY_CONTEN,&CONF_STATUS_PROCESS
                                 JEQ      D_ERRO_EST_UNK_CHAVE_CONTEUDO
                                 CMP.B    #IBYTE_ERRO_SPECIAL_CHARACTER,&CONF_STATUS_PROCESS 
                                 JEQ      D_ERRO_ERRO_SPECIAL_CHARACTER   
                        
                                 JMP      GOGO
D_ERRO_ESTRUTURAL:               CALL     #EXCLUIR_MENSAGEM
                                 CALL     #CRIAR_ERROR_ESTRUTURAL_MSG
                                 JMP      FINGO
D_ERRO_ESTRUTURAL_QTMK:          CALL     #EXCLUIR_MENSAGEM
                                 CALL     #CRIAR_ERROR_ESTRUTURAL_QT_MARKS
                                 JMP      FINGO
D_ERRO_ESTRUTURAL_UNK_JSON:      CALL     #EXCLUIR_MENSAGEM
                                 CALL     #CRIAR_MENSAGEM_JSON_INEXISTENTE
                                 JMP      FINGO
D_ERRO_ESTRUTURAL_COMMA_CLOSING: CALL     #EXCLUIR_MENSAGEM
                                 CALL     #CRIAR_MENSAGEM_VIRGULA_FINALIZACAO_ERRO
                                 JMP  FINGO
D_ERRO_ESTRUTURAL_OB_INC_DUP:    CALL     #EXCLUIR_MENSAGEM
                                 CALL     #CRIAR_MENSAGEM_OBJETO_INTERNO_ERRO
                                 JMP  FINGO                                 

D_ERRO_EST_UNK_CHAVE_CONTEUDO:   CALL     #EXCLUIR_MENSAGEM
                                 CALL     #CRIAR_MENSAGEM_LINHA_NO_CHAVE
                                 JMP  FINGO  

D_ERRO_ERRO_SPECIAL_CHARACTER:   CALL     #EXCLUIR_MENSAGEM
                                 CALL     #CRIAR_MENSAGEM_CARACTER_NAO_COMPATIVEL
                                 JMP  FINGO

GOGO:                            BIC.B    #BIT0,&P1OUT
                                 BIS.B    #BIT6,&P1OUT
                                 CALL     #EXCLUIR_MENSAGEM
                                 CALL     #CRIAR_MENSAGEM_RETORNO_SUCESSO
                                 NOP 

FINGO:                           CALL     #LIMPA_SERIAL_CONTADORES
                                 CALL     #RETORNO_MENSAGEM
                                 BIS.B    #BIT0,&P1OUT
                                 BIC.B    #BIT6,&P1OUT
                                 CALL     #LIMPA_SERIAL_CONTADORES
                                 CALL     #EXCLUIR_MENSAGEM_ENVIADA
                                 MOV.B    #000H,&LEN_SERIAL_MESSAGE
                                 MOV.B    #000H,&LEN_I_MESSAGE
                                 CALL     #LIMPA_SERIAL_CONTADORES
                                 RET                        
;----------------------------------------------------------------------------------------------
;----------------------------------------------------------------------------------------------
        COMMON      INTVEC
;----------------------------------------------------------------------------------------------
                ORG         RESET_VECTOR
                DW          RESET
                ORG         USCIAB0TX_VECTOR
                DW          TRANSMISSAO
                ORG         USCIAB0RX_VECTOR 
                DW          RECEPCAO
        END
;----------------------------------------------------------------------------------------------
